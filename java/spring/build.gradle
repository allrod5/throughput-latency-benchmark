buildscript {
    ext {
        springBootVersion = '2.1.2.RELEASE'
        gradleGitPropertiesPluginVersion = '2.0.0'
        spotbugsPluginVersion = '1.6.9'
        versionsPluginVersion = '0.20.0'
    }
    repositories {
        maven(){
            url "https://nexus.quintoandar.com.br/repository/maven-public/"
        }
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:${gradleGitPropertiesPluginVersion}"
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:${spotbugsPluginVersion}"
        classpath "com.github.ben-manes:gradle-versions-plugin:${versionsPluginVersion}"
    }
}

plugins {
    id "org.sonarqube" version "2.7"
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'pmd'
    apply plugin: 'com.gorylenko.gradle-git-properties'
    apply plugin: "com.github.spotbugs"
    apply plugin: "com.github.ben-manes.versions"

    apply from: "${rootProject.rootDir}/config/pmd.gradle"
    apply from: "${rootProject.rootDir}/config/spotBugs.gradle"
    apply from: "${rootProject.rootDir}/config/checkstyle.gradle"
    apply from: "${rootProject.rootDir}/config/jacoco.gradle"
    apply from: "${rootProject.rootDir}/config/sonarqube/sonar-local.gradle"
    apply from: "${rootProject.rootDir}/config/tests.gradle"

    group = 'br.com.quintoandar'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        maven(){
            url "https://nexus.quintoandar.com.br/repository/maven-public/"
        }
        mavenCentral()
    }

    springBoot {
        buildInfo()
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-starter-aop')

        runtime('org.springframework.boot:spring-boot-devtools')

        testCompile('org.hamcrest:hamcrest-all:1.3')
        testCompile('org.springframework.boot:spring-boot-starter-test')
    }
}

bootJar {
    enabled = false
}

wrapper {
    gradleVersion = '5.2.1'
}

dependencies {
    compile project(':core')
    compile project(':api')
}
